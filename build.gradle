plugins {
    id 'org.jbake.site' version '1.2.0'
}

ext {
    maryVersions = ['5.1', '5.1.1', '5.1.2', '5.2']
}

repositories {
    maven {
        url "https://oss.jfrog.org/artifactory/oss-release-local"
    }
}

maryVersions.each { maryVersion ->
    configurations.create maryVersion
    dependencies.add maryVersion, "de.dfki.mary:marytts:$maryVersion:javadoc"
}

jbake {
    srcDirName = relativePath("$buildDir/generatedContent")
    destDirName = 'site'
    configuration['site.host'] = 'http://marytts.github.io/'
    configuration['render.tags'] = false
    configuration['render.index'] = false
    configuration['render.sitemap'] = true
    configuration['render.feed'] = false
    configuration['render.archive'] = false
//    configuration['markdown.extensions'] = 'ALL'
}

task processAdoc(type: Copy) {
    into "$buildDir/generatedContent/content"
    from 'src/site/adoc', {
        include '**/*.adoc'
        expand project.properties
    }
    from 'src/site/adoc', {
        include '**/*.html'
    }
}

task processAssets(type: Copy) {
    from 'src/site/assets'
    into "$buildDir/generatedContent/assets"
    maryVersions.each { maryVersion ->
        from configurations."$maryVersion", {
            filesMatching '*.jar', { jarFileDetails ->
                copy {
                    from zipTree(jarFileDetails.file)
                    into "$destinationDir/javadoc/$maryVersion"
                }
                jarFileDetails.exclude()
            }
        }
    }
}

task processResources(type: Copy) {
    into "$buildDir/generatedContent/content/download"
    from 'src/site/resources/download', {
        include '**/*.html', '**/*.xml'
    }
}

task processTemplates(type: Copy) {
    from 'src/site/templates'
    into "$buildDir/generatedContent/templates"
}

task bibtex2html(type:Exec) {
    commandLine 'bibtex2html', 'src/site/resources/mary-publications.bib'
    commandLine 'bibtex2html', 'src/site/resources/publications-pavoque.bib'
}

task copyBib {
//    dependsOn bibtex2html
    copy {
        from projectDir
        include "mary-publications*.*"
        into 'src/site/adoc/documentation/publications'
    }
    copy {
        from projectDir
        include "pavoque-publications*.*"
        into 'src/site/adoc/pavoque/publications'
    }
}

bake {
    dependsOn processAdoc, processAssets, processResources, processTemplates
}

assemble {
    dependsOn bake
}

